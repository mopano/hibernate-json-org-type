apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'osgi'

ext {
	expectedGradleVersion = '4.9'
	projName = 'hibernate-json-org-contributor'
	projDescription = 'A type contributor allowing Hibernate to use the commonly known org.json.JSONArray and org.json.JSONObject. Used for PostgreSQL types json/jsonb and MySQL type json.'
}

group = 'com.mopano'
version = '1.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
	mavenLocal()
	mavenCentral()
}

configurations {
	mysqlDetect
}

sourceSets {
	mysqlDetect {
		java.srcDir 'src/mysqlDetect/java'
		resources.srcDir 'src/mysqlDetect/resources'
	}
}


dependencies {
	compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.0.Final'
	compile group: 'org.json', name: 'json', version: '20171018'

	testCompile group: 'junit', name: 'junit', version: '4.10'
	// postgresql driver requires custom version patch to support null values. see pgjdbc.patch
	testRuntime group: 'org.postgresql', name: 'postgresql', version: '42.2.2'

	mysqlDetectCompile sourceSets.main.output
	mysqlDetectCompile configurations.compile
	mysqlDetectCompile group: 'junit', name: 'junit', version: '4.10'
	mysqlDetectRuntime configurations.runtime
	mysqlDetectRuntime group: 'mysql', name: 'mysql-connector-java', version: '8.0.22'
}

jar {
	manifest = null
}

task compile
compile.dependsOn compileJava, processResources, compileTestJava, processTestResources

task testMysqlDetect(type: Test, dependsOn: compile) {
	testClassesDirs = sourceSets.mysqlDetect.output.classesDirs
	classpath = sourceSets.mysqlDetect.runtimeClasspath
}

test.dependsOn testMysqlDetect

task sourcesJar(type: Jar, dependsOn: compileJava) {
	from sourceSets.main.allSource
	classifier = 'sources'
}
task javadocJar(type: Jar, dependsOn: 'javadoc') {
	from javadoc.destinationDir
	classifier = 'javadoc'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar

			// Maven publish plugin adds ALL dependencies with 'runtime' scope
			// see https://github.com/gradle/gradle/blob/b004f39ece183176bf77c1eebc4aa5cbc20ca5cc/subprojects/maven/src/main/java/org/gradle/api/publish/maven/tasks/GenerateMavenPom.java#L104
			pom.withXml {
				Node projNode = asNode()
				projNode.children().each { projSubnode ->
					if (projSubnode.name().getLocalPart().equals('dependencies')) {
						projSubnode.children().each { depnode ->
							if (depnode.name().getLocalPart().equals('dependency')) {
								depnode.children().each { depchild ->
									if (depchild.name().getLocalPart().equals('scope')) {
										depchild.setValue('compile')
									}
								}
							}
						}
					}
				}

				// shamelessly copied from Hibernate build plugin
				projNode.appendNode('name', projName)
				projNode.appendNode('description', projDescription)
				Node licenseNode = projNode.appendNode('licenses').appendNode('license')
				licenseNode.appendNode('name', 'Modified BSD License')
				licenseNode.appendNode('url', 'https://opensource.org/licenses/BSD-3-Clause')

				licenseNode.appendNode('distribution', 'repo')
				projNode.appendNode('url', 'https://github.com/mopano/hibernate-json-org-type')
				Node scmNode = projNode.appendNode('scm')
				scmNode.appendNode('url', 'https://github.com/mopano/hibernate-json-org-type')
				scmNode.appendNode('connection', 'scm:git:https://github.com/mopano/hibernate-json-org-type.git')
				scmNode.appendNode('developerConnection', 'scm:git:git@github.com:mopano/hibernate-json-org-type.git')

				Node developersNode = projNode.appendNode('developers')
				Node developerNode = developersNode.appendNode('developer')
				developerNode.appendNode('id', 'coladict')
				developerNode.appendNode('name', 'Yordan Gigov')
				developerNode.appendNode('organization', 'Mopano')
				developerNode.appendNode('organizationUrl', 'https://mopano.com')
			}
		}
	}
}

model {
	tasks.generatePomFileForMavenJavaPublication {
		destination = file( "$project.buildDir/generated-pom.xml" )
	}
}

wrapper {
	gradleVersion = expectedGradleVersion
}

def dolint = hasProperty("lint") ? getProperty("lint") : null;

if ( dolint != null ) {
	if ( dolint.contains("deprecation") ) {
		gradle.projectsEvaluated {
			tasks.withType(JavaCompile) {
				options.compilerArgs << "-Xlint:deprecation"
			}
		}
	}
	if ( dolint.contains("unchecked") ) {
		gradle.projectsEvaluated {
			tasks.withType(JavaCompile) {
				options.compilerArgs << "-Xlint:unchecked"
			}
		}
	}
}
